# Virtual Feeds Settings
# Copyright by Peter Maersk-Moller 2012 - All rights reserved
#verbose

include ../ini/basis

# Stack (overlay) feed 0. Feed 0 has previously been set to black.
stack  0

# Define some text strings identified by their text id.
text string 0 Snowmix Virtual Feed Setup by Peter Maersk-Moller
text string 1 This is feed #1
text string 2 This is feed #2
text string 3 This is feed #3
text string 4 This is feed #4
text string 5 Find the Snowmix project at http://sourceforge.net/projects/snowmix - Find out more about Copenhagen Suborbitals at http://copenhagensuborbitals.com and http://www.rocketfriends.org and http://www.raketvenner.dk
text string 6 [#RUNTIME,1]
text string 7 [#RUNTIME,2]
text string 8 [#RUNTIME,3]
text string 9 [#RUNTIME,4]
text string 10 [#FEEDSTATE,2,RUNNING]Running 2
text string 11 [#FEEDSTATE,2,PENDING]Pending 2
text string 12 [#FEEDSTATE,2,STALLED]Stalled 2
text string 13 [#FEEDSTATE,2,DISCONNECTED]Lost Signal 2
text string 20 [#DATETIME] CET

# This defines 3 font selections identified by their font id
text font 0 Sans Bold 8
text font 1 Sans Bold 12
text font 2 Sans Bold 14
text font 0 Sans Bold 7

# text place <place id> <text id> <font id> <x> <y> <red> <green> <blue> <achor>
text place 0 0 0 4 -4 1.0 1.0 1.0 1.0 sw
text place align 0 left bottom
text place backgr 0 20 40 2 4 0 0 0 1.0
text place backgr linpat 0 0.00 0.0 0.0 0.0 1.0
text place backgr linpat 0 0.86 0.0 0.0 0.0 1.0
text place backgr linpat 0 0.92 0.0 0.0 0.0 0.5
text place backgr linpat 0 1.00 0.0 0.0 0.0 0.0

command create textplace_feed_4
  # This places the header text for feed 4 aligned at center,top. The 'nw'
  # reference means counting from north-west (0,0). Text background is added
  # extending 20 pixels to the left and right and 2 pixels at the top and bottom.
  # The text background is made out linear pattern defined from 0% over 10%, 50%
  # 90% to 100%. Furthermore the linear pattern is defines as vertical 'v'.
  text place 4 4 2 352 0 1.0 1.0 1.0 1.0 nw
  text place align 4 center top
  text place backgr 4 20 20 2 2 0 0 0 1.0
  text place backgr linpat 4 0.00 .239 .239 .239 0.0 v
  text place backgr linpat 4 0.10 .439 .439 .439 0.7 v
  text place backgr linpat 4 0.50 .839 .839 .839 0.9 v
  text place backgr linpat 4 0.90 .439 .439 .439 0.7 v
  text place backgr linpat 4 1.00 .039 .039 .039 0.0 v

  # This places text id 9 relative to the south-east (right-bottom)
  # corner. Text id 9 is defined as a video timer clock
  text place 9 9 0 5 -19 0.8 0.8 0.8 1.0 sw
  text place align 9 left bottom
  text place backgr 9 5 5 0 0 0 0 0 0.3
command end

command create textplace_feed_1
  # This places the header text for feed 1 aligned at center,top. The 'nw'
  # reference means counting from north-west (0,0). Text background is added
  # extending 15 pixels to the left and right.
  # The text background is made out linear pattern defined from 0% over 10%, 90%
  # to 100%. Furthermore the linear pattern is defines as horizontal 'h' though
  # the letter 'h' has been omitted at the end of the linear pattarn definition
  # as it is the default orientation
  text place 1 1 1 864 0 1.0 1.0 1.0 1.0 nw
  text place align 1 center top
  text place backgr 1 15 15 0 0 0 0 0 1.0
  text place backgr linpat 1 0.00 1.0 0.0 0.0 0.0
  text place backgr linpat 1 0.10 1.0 0.0 0.0 0.5
  text place backgr linpat 1 0.90 1.0 0.0 0.0 0.5
  text place backgr linpat 1 1.00 1.0 0.0 0.0 0.0

  # This places text id 6 relative to the south-east (right-bottom)
  # corner. Text id 6 is defined as a video timer clock
  text place 6 6 0 -64 192 0.8 0.8 0.8 1.0 ne
  text place align 6 left bottom
  text place backgr 6 5 5 0 0 0 0 0 0.3
command end

command create textplace_feed_2
  # This places the header text for feed 2 aligned at center,top. The 'nw'
  # reference means counting from north-west (0,0). Text background is added
  # extending 15 pixels to the left and right.
  # The text background is made out linear pattern defined from 0% over 10%, 90%
  # to 100%. Furthermore the linear pattern is defines as horizontal 'h' though
  # the letter 'h' has been omitted at the end of the linear pattarn definition
  # as it is the default orientation
  text place 2 2 1 864 192 0.2 0.2 0.2 1.0 nw
  text place align 2 center top
  text place backgr 2 15 15 0 0 0 0 0 1.0
  text place backgr linpat 2 0.00 1.0 1.0 0.0 0.0
  text place backgr linpat 2 0.10 1.0 1.0 0.0 0.5
  text place backgr linpat 2 0.90 1.0 1.0 0.0 0.5
  text place backgr linpat 2 1.00 1.0 1.0 0.0 0.0

  # This places text id 7 relative to the south-east (right-bottom)
  # corner. Text id 7 is defined as a video timer clock
  text place 7 7 0 -64 96 0.8 0.8 0.8 1.0 e
  text place align 7 left bottom
  text place backgr 7 5 5 0 0 0 0 0 0.3

  # The next 4 entries places the conditional text. Text id 10-13 has been
  # defined as conditional text showing the feed state.
  text place 10 10 1 860 288 0.4 0.4 0.4
  text place 11 11 1 860 288 0.4 0.4 0.4
  text place 12 12 1 860 288 0.4 0.4 0.4
  text place 13 13 1 860 288 0.4 0.4 0.4
  text place align 10 center middle
  text place align 11 center middle
  text place align 12 center middle
  text place align 13 center middle
command end

command create textplace_feed_3
  # This places the header text for feed 3 aligned at center,top. The 'nw'
  # reference means counting from north-west (0,0). Text background is added
  # extending 15 pixels to the left and right.
  # The text background is made out linear pattern defined from 0% over 10%, 90%
  # to 100%. Furthermore the linear pattern is defines as horizontal 'h' though
  # the letter 'h' has been omitted at the end of the linear pattarn definition
  # as it is the default orientation
  text place 3 3 1 864 384 1.0 1.0 1.0 1.0 nw
  text place align 3 center top
  text place backgr 3 15 15 0 0 0 0 0 1.0
  text place backgr linpat 3 0.00 0.0 1.0 1.0 0.0
  text place backgr linpat 3 0.10 0.0 1.0 1.0 0.5
  text place backgr linpat 3 0.90 0.0 1.0 1.0 0.5
  text place backgr linpat 3 1.00 0.0 1.0 1.0 0.0

  # This places text id 8 relative to the south-east (right-bottom)
  # corner. Text id 8 is defined as a video timer clock
  text place 8 8 0 -64 -19 0.8 0.8 0.8 1.0 se
  text place align 8 left bottom
  text place backgr 8 5 5 0 0 0 0 0 0.3
command end

# This places the very long left scrolling text. The text is placed relative
# to south-east 'se' meaning 1024,576. A flat semi transparent gray'ish text
# back ground is added. Last but not least, the text is set to move 2 pixels
# to the left from its start position until it reaches x=-2700. Then it moves
# back to its start position and starts over.
text place 5 5 1 0 0 0.8 0.8 0.8 1.0 se
text place align 5 left bottom
text place backgr 5 1024 2400 0 0 0 0 0 0.3
text place repeat move 5 -2 0 -2700 0

# This places text id 20 relative to the north-east (right-top)
# corner. Text id 20 is defined as a date and time clock
text place 20 20 0 352 -19 0.8 0.8 0.8 1.0 sw
text place align 20 center bottom
text place backgr 20 15 15 0 0 0 0 0 0.3

# The first 12 frames nothing is done, then 4 texts are moved outside the frame
# and nothing is done for the next 20 frames. Then the text is moved back
# into the frame and the command is looped.
command create Blink_Feed_2_State
  next 12
  text place alpha 10 0.0
  text place alpha 11 0.0
  text place alpha 12 0.0
  text place alpha 13 0.0
  next 20
  text place alpha 10 1.0
  text place alpha 11 1.0
  text place alpha 12 1.0
  text place alpha 13 1.0
  loop
command end

command create create_virtual_feed_1_2_3_4
  virtual feed add 1 Feed #1
  virtual feed source feed 1 1
  virtual feed add 2 Feed #2
  virtual feed source feed 2 2
  virtual feed add 3 Feed #3
  virtual feed source feed 3 3
  virtual feed add 4 Feed #4
  virtual feed source feed 4 4
command end

# Place feed 4 as the main feed and 1,2,3 as the scaled feeds to the right
command create place_virtual_feed_4_1_2_3
  # Feed #1 at 704,0
  virtual feed place rect 1 704 0 320 192 16 48 0.0 0.5 0.5 1.0
  text place 1 1 1 864 0 1.0 1.0 1.0 1.0 nw
  text place coor 6 -64 192 ne

  # Feed #2 at 704,192
  virtual feed place rect 2 704 192 320 192 16 96 0.0 0.5 0.5 1.0
   text place 2 2 1 864 192 0.2 0.2 0.2 1.0 nw
   text place 7 7 0 -64 96 0.8 0.8 0.8 1.0 e
   text place 10 10 1 860 288 0.4 0.4 0.4
   text place 11 11 1 860 288 0.4 0.4 0.4
   text place 12 12 1 860 288 0.4 0.4 0.4
   text place 13 13 1 860 288 0.4 0.4 0.4

  # Feed #2 at 704,384
  virtual feed place rect 3 704 384 320 192 16 96 0.0 0.5 0.5 1.0
   text place 3 3 1 864 384 1.0 1.0 1.0 1.0 nw
   text place 8 8 0 -64 -19 0.8 0.8 0.8 1.0 se

  # Feed 4 at 0,0 in the main window
  virtual feed place rect 4 0 0 704 576 160 0 0.0 1.0 1.0 1.0
   text place 4 4 2 352 0 1.0 1.0 1.0 1.0 nw
   text place coor 9 5 -19 sw
  loop
command end

# Place feed 1 as the main feed and 4,2,3 as the scaled feeds to the right
command create place_virtual_feed_1_4_2_3
  virtual feed place rect 1 0 0 704 576 0 0 0.0 1.0 1.0 1.0
   text place 1 1 2 352 0 1.0 1.0 1.0 1.0 nw
   text place coor 6 5 -19 sw
  virtual feed place rect 2 704 192 320 192 16 96 0.0 0.5 0.5 1.0
   text place 2 2 1 864 192 0.2 0.2 0.2 1.0 nw
   text place 7 7 0 -64 96 0.8 0.8 0.8 1.0 e
   text place 10 10 1 860 288 0.4 0.4 0.4
   text place 11 11 1 860 288 0.4 0.4 0.4
   text place 12 12 1 860 288 0.4 0.4 0.4
   text place 13 13 1 860 288 0.4 0.4 0.4
  virtual feed place rect 3 704 384 320 192 16 96 0.0 0.5 0.5 1.0
   text place 3 3 1 864 384 1.0 1.0 1.0 1.0 nw
   text place 8 8 0 -64 -19 0.8 0.8 0.8 1.0 se
  virtual feed place rect 4 704 0 1024 576 0 0 0.0 0.3333333333 0.3333333333 1.0
   text place 4 4 1 864 0 1.0 1.0 1.0 1.0 nw
   text place coor 9 -64 192 ne
  loop
command end

# Place feed 2 as the main feed and 1,4,3 as the scaled feeds to the right
command create place_virtual_feed_2_1_4_3
  # Feed #1 at 704,0
  virtual feed place rect 1 704 0 320 192 16 48 0.0 0.5 0.5 1.0
  text place 1 1 1 864 0 1.0 1.0 1.0 1.0 nw
  text place coor 6 -64 192 ne

  # Feed #2 at 704,192
  virtual feed place rect 2 0 0 704 576 0 0 0.0 1.0 1.0 1.0
   text place 2 4 1 864 192 0.2 0.2 0.2 1.0 nw
   #text place 7 7 0 -64 96 0.8 0.8 0.8 1.0 e
   text place coor 7 5 -19 sw
   text place 10 10 1 352 288 0.4 0.4 0.4
   text place 11 11 1 352 288 0.4 0.4 0.4
   text place 12 12 1 352 288 0.4 0.4 0.4
   text place 13 13 1 352 288 0.4 0.4 0.4

  # Feed #2 at 704,384
  virtual feed place rect 3 704 384 320 192 16 96 0.0 0.5 0.5 1.0
   text place 3 3 1 864 384 1.0 1.0 1.0 1.0 nw
   text place 8 8 0 -64 -19 0.8 0.8 0.8 1.0 se

  # Feed 4 at 0,0 in the main window
  virtual feed place rect 4 704 192 1024 576 0 0 0.0 0.333333333 0.333333333 1.0
   text place 4 2 2 352 0 1.0 1.0 1.0 1.0 nw
   text place coor 9 -64 96 e
  loop
command end

# This will make the text grow from left to right
command create grow_text
  next 100
  text place grow 0 0 5
  next 100
  text place grow 1 1
  next 100
  text place grow 2 1
  next 100
  text place grow 3 1
  next 100
  text place grow 4 1
  loop
command end

# Run the next two command to create the virtual feeds and to place them
textplace_feed_1
textplace_feed_2
textplace_feed_3
textplace_feed_4
create_virtual_feed_1_2_3_4
place_virtual_feed_4_1_2_3

command create change_feed_places
  place_virtual_feed_4_1_2_3
  next 24
  text place 21
  next 240
  text string 22 Swopping feed #2 and #4
  text place 22 22 2 352 200 0.0 0.0 0.0 1.0 nw
  text place align 22 center middle
  text place backgr 22 5 5 0 0 1 1 1 0.4
  next 48
  place_virtual_feed_2_1_4_3
  text string 22 Feed #2 and #4 was swopped
  next 24
  text place 22
  next 240
  text string 22 Swopping feed #2 and #4 back
  text place 22 22 2 352 200 0.0 0.0 0.0 1.0 nw
  text place align 22 center middle
  text place backgr 22 5 5 0 0 1 1 1 0.4
  next 48
  place_virtual_feed_4_1_2_3
  text string 22 Feed #2 and #4 was swopped back
  next 24
  text place 22
  next 240
  text string 21 Swopping feed #1 and #4
  text place 21 21 2 352 200 0.0 0.0 0.0 1.0 nw
  text place align 21 center middle
  text place backgr 21 5 5 0 0 1 1 1 0.4
  next 48
  place_virtual_feed_1_4_2_3
  text string 21 Feed #1 and #4 was swopped
  next 24
  text place 21
  next 240
  text string 21 Swopping feed #1 and #4 back
  text place 21 21 2 352 200 0.0 0.0 0.0 1.0 nw
  text place align 21 center middle
  text place backgr 21 5 5 0 0 1 1 1 0.4
  next 48
  text string 21 Feed #1 and #4 was swopped back
  loop
command end

# This is the definition of the command 'Show'. The command (script)
# says first to execute command (script) Blink_Feed_2_State which is defined
# below. It then overlays text place id 1 and 3, then 2 and 4 until end of
# text place ids. Then text place id is placed last to place it ontop of
# other text, in this case to overlay it the scrolling text
# The 'loop' command is essential and tells Snowmix to loop the command,
# to point at the start of the command before next frame..
command create Show
  change_feed_places
  overlay virtual feed 1
  overlay virtual feed 2..4
  overlay image 0
  Blink_Feed_2_State
  grow_text
  overlay text 1 3
  overlay text 2 4..end
  overlay text 0
  loop
command end

# >>>>>>>>>>>>>>>>>> I M P O R T A N T <<<<<<<<<<<<<<<<<<<<<<<
# This command tells Snowmix to run the command 'Show' for each frame after
# feeds have been stacked as defined by the 'stack command'
overlay finish Show
# >>>>>>>>>>>>>>>>>> I M P O R T A N T <<<<<<<<<<<<<<<<<<<<<<<
#monitor on
